/*
 * PerlOnJava Build Configuration
 * This Gradle build script configures the build process for the PerlOnJava project
 */

buildscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
    }
}

// Core plugins configuration
plugins {
    id 'java'
    // Plugin for updating version catalog with latest versions (configuration cache compatible!)
    alias(libs.plugins.version.catalog.update)
    id 'application'
    // Plugin for creating OS packages (deb)
    alias(libs.plugins.ospackage)
    // Plugin for creating fat/uber JARs
    alias(libs.plugins.shadow)
}

// Main application class configuration
application {
    mainClass = 'org.perlonjava.Main'
}

// Debian package build dependency
tasks.buildDeb {
    dependsOn installDist
}

// Project metadata
group = 'org.perlonjava'
version = '3.0.0'

// OS package configuration for Debian packaging
ospackage {
    packageName = 'perlonjava'
    version = project.version
    maintainer = 'Flavio Soibelmann Glock <fglock@gmail.com>'
    // Java 21+ is required at runtime (any distribution: Oracle, Azul, Temurin, OpenJDK, etc.)

    into '/opt/perlonjava'
    from('build/install/perlonjava') {
        into '/opt/perlonjava'
    }

    link('/usr/local/bin/jperl', '/opt/perlonjava/bin/jperl')
}

// Java toolchain configuration - requires Java 21
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

// Repository configuration
repositories {
    mavenCentral()
}

// Project dependencies
dependencies {
    // Core dependencies
    implementation libs.asm                           // ByteCode manipulation
    implementation libs.asm.util                      // ASM utilities
    implementation libs.icu4j                         // Unicode support
    implementation libs.fastjson2                     // JSON processing
    implementation libs.snakeyaml.engine              // YAML processing
    implementation libs.tomlj                         // TOML processing
    implementation libs.commons.csv                   // CSV processing
    implementation libs.jna                           // Native access
    implementation libs.jna.platform                  // Native access

    // Testing dependencies
    testImplementation libs.junit.jupiter.api
    testImplementation libs.junit.jupiter.engine
    testImplementation libs.junit.jupiter.params
}

// JUnit configuration
testing {
    suites {
        test {
            useJUnitJupiter()
        }
    }
}

// Java compilation settings
tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << '-Xlint:-options'
    options.compilerArgs << '-Xlint:deprecation'
}

// Test execution configuration with native access
tasks.withType(Test).configureEach {
    jvmArgs += '--enable-native-access=ALL-UNNAMED'
}

// Enable native access for all Java execution tasks
allprojects {
    tasks.withType(JavaExec).configureEach {
        jvmArgs += '--enable-native-access=ALL-UNNAMED'
    }
}

// JUnit platform configuration - default test task runs only unit tests
test {
    useJUnitPlatform {
        includeTags 'unit'
        excludeTags 'full'
    }
}

// Fast unit tests only (tests in unit/ directory)
tasks.register('testUnit', Test) {
    description = 'Runs fast unit tests only (from unit/ directory)'
    group = 'verification'
    
    useJUnitPlatform {
        includeTags 'unit'
        excludeTags 'full'
    }
    
    shouldRunAfter test
}

// All tests including comprehensive module tests
tasks.register('testAll', Test) {
    description = 'Runs all tests including comprehensive module tests'
    group = 'verification'
    
    useJUnitPlatform {
        includeTags 'full'
    }
    
    shouldRunAfter testUnit
}

// Shadow JAR configuration for creating standalone executable
shadowJar {
    archiveClassifier.set('')
    destinationDirectory = file("$buildDir/../target")
    manifest {
        attributes 'Main-Class': 'org.perlonjava.Main'
    }
    exclude 'module-info.class'
    exclude 'META-INF/MANIFEST.MF'
}

// Make shadowJar part of the build process
tasks.named('build') {
    dependsOn shadowJar
}

// Source sets configuration for including Perl resources
sourceSets {
    main {
        resources {
            srcDir 'src/main/perl'
            srcDir 'src/main/resources'
            include '**/*.pm'
            include '**/*.pl'
            include 'META-INF/services/**'
        }
    }
    test {
        resources {
            srcDir 'src/test/resources'
        }
    }
}

// Resource processing configuration
tasks.named('processResources', Copy) {
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.pm'
        include 'META-INF/services/**'
    }
    into("$buildDir/resources/main")
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

// Test resource processing configuration
tasks.named('processTestResources', Copy) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

// Parallel test execution tasks
// Run with: ./gradlew testUnitParallel --parallel
def parallelShards = 4

(0..<parallelShards).each { index ->
    tasks.register("testUnitShard${index}", Test) {
        group = 'verification'
        description = "Runs shard ${index} of ${parallelShards} of unit tests"
        
        useJUnitPlatform {
            includeTags 'unit'
            excludeTags 'full'
        }
        
        systemProperty 'test.shard.index', index
        systemProperty 'test.shard.total', parallelShards
        
        testClassesDirs = sourceSets.test.output.classesDirs
        classpath = sourceSets.test.runtimeClasspath
    }
}

tasks.register('testUnitParallel') {
    group = 'verification'
    description = 'Runs unit tests in parallel across multiple JVMs. Usage: gradle testUnitParallel --parallel'
    dependsOn 'testUnitShard0', 'testUnitShard1', 'testUnitShard2', 'testUnitShard3'
}

// Version catalog update configuration
// The nl.littlerobots.version-catalog-update plugin is configuration cache compatible!
// Use: ./gradlew versionCatalogUpdate to check and update dependencies
// Use: ./gradlew versionCatalogUpdate --interactive to review changes interactively
versionCatalogUpdate {
    // Sort the catalog keys
    sortByKey = true
    // Keep versions not used in the project
    keep {
        keepUnusedVersions = true
    }
}
