plugins {
    id 'java'
    id 'jvm-test-suite'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'org.perlonjava'
version = '1.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.ow2.asm:asm:9.7'
    implementation 'org.ow2.asm:asm-util:9.7'
    implementation 'com.ibm.icu:icu4j:71.1'
}

testing {
    suites {
        test {
            useJUnitJupiter('5.9.2')
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << '-Xlint:-options'
    options.compilerArgs << '-Xlint:deprecation'
}

shadowJar {
    archiveClassifier.set('')
    destinationDirectory = file("$buildDir/../target")
    manifest {
        attributes 'Main-Class': 'org.perlonjava.Main'
    }
    exclude 'module-info.class'
    exclude 'META-INF/MANIFEST.MF'
}

tasks.named('build') {
    dependsOn shadowJar
}

sourceSets {
    main {
        resources {
            srcDir 'src/main/perl'
            include '**/*.pm'
        }
    }
    test {
        resources {
            srcDir 'src/test/resources'
        }
    }
}

tasks.named('processResources', Copy) {
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.pm'
    }
    into("$buildDir/resources/main")
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.named('processTestResources', Copy) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
